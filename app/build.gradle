apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.ujujzk.vrgweather"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            Properties properties = new Properties()
            properties.load(project.rootProject.file('apikey.properties').newDataInputStream())
            def apiKey = properties.getProperty('openweather_app_id')
            buildConfigField "String", "OPEN_WEATHER_APP_ID", apiKey
        }
        debug {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('apikey.properties').newDataInputStream())
            def apiKey = properties.getProperty('openweather_app_id')
            buildConfigField "String", "OPEN_WEATHER_APP_ID", apiKey
        }


    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

}


buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

dependencies {

    ext.supportVersion = '25.3.1'
    ext.daggerVersion='2.9'
    ext.retrofitVersion='2.2.0'
    ext.chuckVersion='1.0.4'

    //internal
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //support
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    //networking
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    debugCompile "com.readystatesoftware.chuck:library:$chuckVersion" //HTTP inspector
    releaseCompile "com.readystatesoftware.chuck:library-no-op:$chuckVersion"

    //utils
    compile 'com.orhanobut:logger:1.15'
    compile('com.github.ajitsing:sherlock:1.0.1@aar') {
        transitive = true
    }
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0' //needed for Dagger
    provided 'org.glassfish:javax.annotation:10.0-b28'  //needed for Dagger

    //rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    //compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'
    //compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:1.0.0'

    //tests
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
